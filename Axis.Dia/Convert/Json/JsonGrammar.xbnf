# Dia-flavored Json
#

# Root
$root -> ?[$object $array]


# Spaces
$block-space -> ?[$char-space].+
$char-space -> ?[$space $tab $new-line $carriage-return $form-feed $vertical-tab]
$space -> ' '
$tab -> '\t'
$new-line -> '\n'
$carriage-return -> '\r'
$form-feed -> '\f'
$vertical-tab -> '\v'
$line-space -> ?[$tab $space]


# Array
$array -> +[
	'[' +[$block-space.? $meta-item $block-space.? ','].? +[
		$block-space.?
		$json-value +[
			$block-space.?
			','
			$block-space.?
			$json-value
		].*
	].?
	$block-space.? ']'
]
$meta-item -> +['"' '0$' $container-metadata '"']


# Object
$object -> +[
	'{' +[
		$block-space.?
		?[$meta-property $object-field] +[
			$block-space.?
			','
			$block-space.?
			$object-field
		].*
	].?
	$block-space.? '}'
]
$object-field -> +[$field-name $block-space.? ':' $block-space.? $json-value]
$field-name -> +[ '"' $name-metadata.? $value-content '"']
$meta-property -> +['"0$"' $block-space.? ':' $block-space.? '"' $container-metadata '"']


# Json Value
$json-value -> ?[$null $number-value $bool-value $encoded-value $string-value $array $object]


# Name Metadata
$name-metadata -> +['[' $annotation-list ']']


# Value Metadata
$value-metadata -> +['[' $line-space.*
	$ref-index.? $line-space.*
	$declared-type $line-space.*
	$annotation-list.? $line-space.* ']']


# Container Metadata
$container-metadata -> +['[' $line-space.*
	?[
		$annotation-list
		+[
			$ref-index $line-space.*
			$annotation-list.? $line-space.*
		 ]
	 ] ']']


# Type Declaration
$declared-type -> +['$' $types ';']
$types -> ?['Bool' 'Int' 'Decimal' 'Instant' 'Blob' 'Clob' 'String' 'Symbol' 'List' 'Record' 'Ref']


# Ref Index
$ref-index -> +['#' $hex-int ';']
$hex-int -> /^0[xX][0-9A-Fa-f]+\z/.3,+


# Annotation List
$annotation-list -> +[$annotation ';' $line-space.*].+
$annotation -> ?[$identifier $quoted-text]
$quoted-text -> @SQText
$identifier -> @Identifier


# Bool
$bool-value -> ?['true' 'false']


# Number Value
$number-value -> ?[$scientific-decimal $regular-decimal $int-number]

$int-number -> +[ $negative-sign.? $int-digits]
$int-digits -> /^\d+\z/

$regular-decimal -> +[$int-number '.' $int-digits.?]

$scientific-decimal -> +[
	$int-number
	+['.' $int-digits].?
	'E' $exponent-sign.? $int-digits]
$exponent-sign -> ?['+' '-']
$negative-sign -> '-'


# Null
$null -> 'null'


#  Encoded Value
$encoded-value -> +['"' $value-metadata $line-space.* ?[$time-stamp $value-content] '"']

$time-stamp -> ?[
	$millisecond-precision $second-precision
	$minute-precision $day-precision $month-precision
	$year-precision]
$millisecond-precision -> +[
	$year '-' $month '-' $day $date-delimiter
	$hour ':' $minute ':' $second '.'
	$millisecond $time-zone-offset]>12
$second-precision -> +[$year '-' $month '-' $day $date-delimiter $hour ':' $minute ':' $second $time-zone-offset]>10
$minute-precision -> +[$year '-' $month '-' $day $date-delimiter $hour ':' $minute $time-zone-offset]>7
$day-precision -> +[$year '-' $month '-' $day $date-delimiter.?]
$month-precision -> +[$year '-' $month $date-delimiter]
$year-precision -> +[$year $date-delimiter]
$time-zone-offset -> ?[$utc-offset $minute-precision-offset]
$minute-precision-offset -> +[?['+' '-'] $hour ':' $minute]
$utc-offset -> "Z"
$date-delimiter -> "T"
$year -> /^\d{4,}\z/.4,+
$month -> /^(01|02|03|04|05|06|07|08|09|10|11|12)\z/.2,+
$day -> /^(01|02|03|04|05|06|07|08|09|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31)\z/.2,+
$hour -> /^(00|01|02|03|04|05|06|07|08|09|10|11|12|13|14|15|16|17|18|19|20|21|22|23)\z/.2,+
$minute -> /^[0-5]\d\z/.2,+
$second -> /^[0-5]\d\z/.2,+
$millisecond -> /^\d+\z/


# String Value
$string-value -> +['"' $value-content '"']
$value-content -> @StringContent
